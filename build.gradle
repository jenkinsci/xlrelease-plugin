// :copyright: (c) 2019 by XebiaLabs BV.
// :license: GPLv2, see LICENSE for more details.

buildscript {
    repositories {
        maven {
            name "jenkins"
            delegate.url("http://repo.jenkins-ci.org/releases")
        }
        // The plugin is currently only available via the Jenkins
        // Maven repository, but has dependencies in Maven Central.
        mavenCentral()

    }
    dependencies {
        // classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.28.1'
        classpath 'net.researchgate:gradle-release:2.3.4'
    }
}

plugins {
    id "org.jenkins-ci.jpi" version "0.28.0"
    id "net.researchgate.release" version "2.6.0"
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group = "com.xebialabs.ci"
version = "7.5.5-SNAPSHOT"
description = "Package and deploy your applications from Jenkins with &lt;a href='http://www.xebialabs.com'&gt;XebiaLabs XL Release&lt;/a&gt;."

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'

build.dependsOn jpi

task deleteGeneratedSrcLocalizer(type:Delete) {
    delete "build/generated-src/localizer"
}

localizer.dependsOn deleteGeneratedSrcLocalizer

repositories {
    mavenCentral()
    maven {
        url "${projectDir}/repo"
    }
    maven {
        name "netbeans"
        delegate.url("http://bits.netbeans.org/maven2")
    }
    maven {
        name "jenkins"
        delegate.url("http://repo.jenkins-ci.org/releases")
    }
}

jenkinsPlugin {
    coreVersion = '2.150.1' // we need to support LTS versions
    displayName = "XebiaLabs XL Release Plugin"
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/XL+Release+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/XL+Release-plugin'

    developers {
        developer {
            id 'XebiaLabsCI'
            name 'XebiaLabs'
            email 'xl-developers (at) xebialabs (dot) com'
        }
    }
}

configurations {
    compile.transitive = true

    all*.exclude group: 'com.thoughtworks.xstream', module: "xstream"
    // this artifact lives in a netbeans repository which behaves flaky
    // it's not used so exclude it.
    testCompile.exclude group: 'org.netbeans.modules', module: "org-netbeans-insane"

    additionalLibs
}

testSets {
    itest
}

// Integration test configuration which runs using a real XL Release server
sourceSets {
    itest {
        resources {
            srcDir file('src/itest/resources')
        }
    }
}

// Below dependencies should have been inherited from testRuntime
dependencies {
    itestCompile "org.jenkins-ci.plugins:scm-api:2.2.2@jar"
    itestCompile "org.jenkins-ci.plugins:script-security:1.34@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-api:2.23.1@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-cps:2.41@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-job:2.15@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-scm-step:2.6@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-step-api:2.13@jar"
    itestCompile "org.jenkins-ci.plugins.workflow:workflow-support:2.16@jar"

    itestCompile "org.jenkins-ci.main:jenkins-test-harness:2.48"
    itestCompile "com.cloudbees:groovy-cps:1.24"
    itestCompile "org.kohsuke:groovy-sandbox:1.19"
    itestCompile "org.jboss.marshalling:jboss-marshalling:2.0.6.Final"
    itestCompile "org.jboss.marshalling:jboss-marshalling-river:2.0.6.Final"
    itestCompile "org.antlr:antlr4-runtime:4.7.2"

    itestCompile "net.sourceforge.htmlunit:htmlunit:2.34.1"
    itestCompile "net.sourceforge.htmlunit:htmlunit-cssparser:1.3.0"
    itestCompile "junit:junit:4.11"
}

dependencies {
    compile "com.sun.jersey:jersey-client:1.18"
    compile "com.sun.jersey:jersey-core:1.18"
    compile "com.sun.jersey:jersey-json:1.18"
    compile "org.jdom:jdom2:2.0.5"
    compile "org.slf4j:slf4j-api:1.7.5"
    compile "javax.ws.rs:jsr311-api:1.1.1"
    compile "javax.xml.bind:jaxb-api:2.2.2"
    compile "com.google.code.gson:gson:2.8.5"

    jenkinsPlugins "org.jenkins-ci.plugins:credentials:2.1.18@jar"
    jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2+@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.7@hpi'
    jenkinsPlugins 'org.jenkins-ci.plugins:structs:1.10'

    testCompile "org.mockito:mockito-core:2.25.0"
}

if (project.hasProperty('xlReleaseIntegration.host')) {
    def host = project.property('xlReleaseIntegration.host')
    def username = project.property('xlReleaseIntegration.username')
    def password = project.property('xlReleaseIntegration.password')
    itest.systemProperties([
        "xlReleaseIntegration.host" : "$host",
        "xlReleaseIntegration.username" : "$username",
        "xlReleaseIntegration.password" : "$password"
    ])
}

import org.apache.tools.ant.filters.ReplaceTokens

processItestResources {
    filesMatching('**/com.xebialabs.xlrelease.ci.XLReleaseNotifier.xml') {
        filter ReplaceTokens, tokens: [
            'xlReleaseServerUrl': project.property('xlReleaseIntegration.host')
        ]
    }
}

release {
    buildTasks = ['publish']
    tagTemplate = 'xlrelease-plugin-$version'
    preTagCommitMessage = 'Released Jenkins XL Release plugin '
    tagCommitMessage = 'Tag for xlrelease-plugin-$version'
    newVersionCommitMessage = 'Set development version to '
}
